buildscript {
    repositories {
        maven { url 'https://dl.bintray.com/platan/gradle-plugins' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.platan.gradle.plugin:coveralls-gradle-plugin:2.4.0x:jdk6'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'idea'
    id 'checkstyle'
    id 'codenarc'
    id 'me.champeau.gradle.jmh' version '0.2.0'
    id 'org.jetbrains.intellij' version '0.0.43'
}

apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'kotlin'

sourceCompatibility = 1.6
targetCompatibility = 1.6

group 'com.github.platan'
project.version = '0.5.1'

repositories {
    mavenCentral()
}
ext {
    ideaVersion = project.hasProperty('ideaVersion') ? project.getProperty('ideaVersion') : ideaVersion
}

intellij {
    version = ideaVersion
    type = 'IC'
    plugins = ['Groovy', 'gradle', 'properties', 'junit']
    downloadSources = false
    updateSinceUntilBuild = false
}

String spockVersion() {
    def ideaVersion = getIdeaMainVersion()
    def ideaToGroovy = ['14.0': '2.2', '14.1': '2.3', '15.0': '2.3']
    def groovyToSpock = ['2.0': '2.0', '2.1': '2.0', '2.2': '2.0', '2.3': '2.3', '2.4': '2.4']
    def groovyForSpock = groovyToSpock.get(ideaToGroovy[ideaVersion], '2.4')
    if (!groovyForSpock) {
        throw new GradleException("Cannot resolve Spock version for Intellij IDEA version ${ideaVersion}!")
    }
    "1.0-groovy-${groovyForSpock}"
}

private String getIdeaMainVersion() {
    String version = ideaVersion
    if (version.startsWith('LATEST'))
        return 'LATEST'
    def versionMatcher
    if ((versionMatcher = version =~ /^(?:(?:IU|UC)-)?(\d+\.\d+)\.\d+/)) {
        return versionMatcher.group(1)
    }
    if ((versionMatcher = version =~ /^(?:(?:IU|UC)-)?(\d+\.\d+)/)) {
        return versionMatcher.group(1)
    }
    throw new GradleException('Cannot parse IntelliJ IDEA version: ' + version)
}

dependencies {
    compile files("src/main/resources")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    testCompile("org.spockframework:spock-core:${spockVersion()}") {
        exclude module: 'groovy-all'
    }
}

idea {
    module {
        scopes.TEST.plus += [configurations.jmh]
    }
}

codenarc {
    configFile = file("${project.rootDir}/config/codenarc/rules.groovy")
    toolVersion = codenarcToolVersion
}

jacoco {
    toolVersion = jacocoToolVersion
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

checkstyle {
    toolVersion = checkstyleToolVersion
    configFile = file("${project.rootDir}/config/checkstyle/checks.xml")
}

test {
    jvmArgs '-XX:MaxPermSize=256m'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
